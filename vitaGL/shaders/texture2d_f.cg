float4 main(
	float2 vTexcoord : TEXCOORD0,
	uniform sampler2D tex,
	uniform float alphaCut,
	uniform int alphaOp,
	uniform float4 tintColor,
	uniform int texEnv,
	uniform float4 texEnvColor
	)
{
	float4 texColor = tex2D(tex, vTexcoord);
	
	// Texture Environment
	if (texEnv < 3){
		if (texEnv == 0){ // GL_MODULATE
			texColor = texColor * tintColor;
		}else if (texEnv == 1){ // GL_DECAL
			texColor.rgb = lerp(tintColor.rgb, texColor.rgb, texColor.a);
			texColor.a = tintColor.a;
		}else{ // GL_BLEND
			texColor.rgb = lerp(tintColor.rgb, texEnvColor.rgb, texColor.rgb);
			texColor.a = texColor.a * tintColor.a;
		}
	}
	
	// Alpha Test
	if (alphaOp < 7){
		if (alphaOp == 0){
			if (texColor.a < alphaCut){
				discard;
			}
		}else if (alphaOp == 1){
			if (texColor.a <= alphaCut){
				discard;
			}
		}else if (alphaOp == 2){
			if (texColor.a == alphaCut){
				discard;
			}
		}else if (alphaOp == 3){
			if (texColor.a != alphaCut){
				discard;
			}
		}else if (alphaOp == 4){
			if (texColor.a > alphaCut){
				discard;
			}
		}else if (alphaOp == 5){
			if (texColor.a >= alphaCut){
				discard;
			}
		}else{
			discard;
		}
	}
	
	return texColor;
}
